/*

This is the pathmap editor used for customizing pathmaps.

You can quickly copy the compiled code by pressing ctrl+shift+p and running the 'Overwatch Script To Workshop: Copy pathmap editor code' command
in vscode with the Overwatch Script To Workshop extension.

*/

import "!Container.del";

rule: "Commands" -1
{
    DisableInspectorRecording();
    CreateHudText(VisibleTo:AllPlayers(), Text:"https://github.com/ItsDeltin/Overwatch-Script-To-Workshop/wiki/Pathfinding", TextColor:Color.Blue, Location:Location.Right, SortOrder:0);
    HelpText("Ultimate: Create node."                                                    , 1, Color.SkyBlue);
    HelpText("Crouch + Ultimate: Destroy node."                                          , 2, Color.SkyBlue);
    HelpText("Interact: Select node."                                                    , 3, Color.LimeGreen);
    HelpText("Crouch + Interact: Deselect all nodes."                                    , 4, Color.LimeGreen);
    HelpText("Secondary Fire: Connect all selected nodes."                               , 5, Color.Green);
    HelpText("Crouch + Secondary Fire: Disconnect all selected nodes."                   , 6, Color.Green);
    HelpText("Voice Line Right: Create a path from the first to the last selected nodes.", 7, Color.Green);
    HelpText("Voice Line Left: Connect all selected nodes to the first selected node."   , 8, Color.Green);
    HelpText("Ability 1: Switch primary attribute."                                      , 9, Color.Yellow);
    HelpText("Crouch + Ability 1: Switch primary attribute."                             , 10, Color.Yellow);
    HelpText("Ability 2: Switch secondary attribute."                                    , 11, Color.Orange);
    HelpText("Crouch + Ability 2: Switch secondary attribute."                           , 12, Color.Orange);
    HelpText("Reload: Toggle free walk."                                                 , 13, Color.Turquoise);
    HelpText("Melee: Compile pathmap."                                                   , 14, Color.Turquoise);

    CreateHudText(AllPlayers(), Text: "OSTW pathmap editor", Location:Location.Left, SortOrder: 0);
    CreateHudText(AllPlayers(), Text: "Remember to periodically compile your pathmap so you don't lose progress.", TextColor: Color.Orange, Location:Location.Left, SortOrder: 1);

    CreateHudText(
        VisibleTo: AllPlayers(),
        Header   : "Primary Attribute:",
        Subheader: ["Normal", "Custom"][Min(FirstAttribute, 1)],
        Text     : FirstAttribute,
        TextColor: Color.Yellow,
        Location : Location.Top,
        SortOrder: 1
    );
    CreateHudText(
        VisibleTo: AllPlayers(),
        Header   : "Secondary Attribute:",
        Subheader: ["Normal", "Custom"][Min(SecondAttribute, 1)],
        Text     : SecondAttribute,
        TextColor: Color.Orange,
        Location : Location.Top,
        SortOrder: 2
    );
    CreateHudText(FreeWalk ? AllPlayers() : null, Header: "Free Walk Enabled", Location: Location.Top, SortOrder: 3);
}

globalvar define scoper;
globalvar Node[] Nodes = [];
globalvar Node[] Selected = [];
globalvar Segment[] Segments = [];
globalvar define updateVisibleLook;
globalvar define updateVisiblePosition;
globalvar define compiling = false;
globalvar Vector[] nodesOut    3;
globalvar Vector[] segmentsOut 4;
globalvar Vector[] preloadNodes 5;
globalvar Vector[] preloadSegments 6;
globalvar define FirstAttribute = 0;
globalvar define SecondAttribute = 0;
globalvar define FreeWalk = false;

rule: "Create Scoper"
{
    if (CurrentGameMode() == GameMode.Deathmatch)
        CreateDummyBot(Hero.Roadhog, Team.All, 1, Vector(0, 1000, 0));
    else
        CreateDummyBot(Hero.Roadhog, Team.Team1, 1, Vector(0, 1000, 0));
    scoper = LastCreatedEntity();
    SetGravity(scoper, 0);
    SetStatus(scoper, null, Status.Unkillable, 9999);
    SetMaxHealth(scoper, 1000);
    StartDamageModification(scoper, FilteredArray(AllPlayers(), ArrayElement() != scoper), 0, DamageModificationRev.ReceiversDamagersAndDamagePercent);
    SetInvisible(scoper, InvisibleTo.All);
}

void HelpText(ref define text, ref define order, Color color)
{
    CreateHudText(AllPlayers(), Text:text, TextColor:color, Location:Location.Right, SortOrder:order);
}

rule: "Disable conflicting controls"
Event.OngoingPlayer
if (!FreeWalk)
{
    SetAbility1Enabled(EventPlayer(), false);
    SetAbility2Enabled(EventPlayer(), false);
    SetSecondaryFireEnabled(EventPlayer(), false);
    SetUltimateAbilityEnabled(EventPlayer(), false);
}
rule: "Enable conflicting controls"
Event.OngoingPlayer
if (FreeWalk)
{
    SetAbility1Enabled(EventPlayer(), true);
    SetAbility2Enabled(EventPlayer(), true);
    SetSecondaryFireEnabled(EventPlayer(), true);
    SetUltimateAbilityEnabled(EventPlayer(), true);
}
rule: "Toggle freewalk"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), Button.Reload))
{
    FreeWalk = !FreeWalk;
    if (FreeWalk) SmallMessage(AllPlayers(), "Free walk enabled.");
    else SmallMessage(AllPlayers(), "Free walk disabled.");
}

Node GetClosestNode(): Nodes.SortedArray(Node node => EyePosition(EventPlayer()).DistanceTo(node.ShownLocation))[0];
define IsNodeSelectable(Node node): DistanceBetween(node.Pos, EyePosition(EventPlayer())) < 5;

rule: "Create node"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.Ultimate))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    Nodes = Append(Nodes, new Node(PositionOf(EventPlayer())));
    (<Node>LastOf(Nodes)).Show();
}

rule: "Delete node"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.Ultimate))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    Node selected = GetClosestNode();
    if (!IsNodeSelectable(selected)) return;

    for (define i = Segments.Length - 1; i >= 0; i--)
        if (Segments[i].Nodes.Contains(selected))
        {
            (<Segment>Segments[i]).Hide();
            delete(<Segment>Segments[i]);
            Segments = RemoveFromArrayAtIndex(Segments, i);
        }
    PlayEffect(AllPlayers(), PlayEffect.BadExplosion, Color.Blue, selected.ShownLocation, 0.25);
    selected.Hide();
    Selected = RemoveFromArray(Selected, selected);
    Nodes = RemoveFromArray(Nodes, selected); 
    delete(selected);
}

rule: "Select"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.Interact))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    Node selected = GetClosestNode();
    if (!IsNodeSelectable(selected)) return;

    if (!selected.Selected)
    {        
        selected.Select();
        Selected = Append(Selected, selected);
    }
    else
    {
        selected.Deselect();
        Selected = RemoveFromArray(Selected, selected);
    }
}

rule: "Deselect all"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.Interact))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    foreach (Node selected in Selected)
        selected.Deselect();
    Selected = EmptyArray();
}

class Node
{
    public define Pos;
    public define ShownLocation;
    private define EffectID;
    public define Selected = false;
    public define visible = false;
    public define SelectIDText;

    public Node(define pos)
    {
        Pos = pos;
        ShownLocation = Pos + Vector(0, 1.25, 0);
    }

    public void Show()
    {
        if (!visible)
        {
            Update();
            visible = true;
        }
    }

    public void Hide()
    {
        if (visible)
        {
            DestroyInWorldText(SelectIDText);
            DestroyEffect(EffectID);
            visible = false;
        }
    }
    
    public void Select()
    {
        Selected = true;
        Update();
    }

    public void Deselect()
    {
        DestroyInWorldText(SelectIDText);
        Selected = false;
        Update();
    }

    private void Update()
    {
        DestroyEffect(EffectID);
        if (!Selected)
            Make(Color.Blue);
        else
        {
            Make(Color.Green);
            root.ActivateScoper(root.scoper, 1, this);
        }
        EffectID = LastCreatedEntity();
    }

    private void Make(Color color)
    {
        CreateEffect(AllPlayers(), Effect.Sphere, color, ShownLocation, 0.25, EffectRev.VisibleTo);
    }

    public void UpdateVisible()
    {
        if (DistanceBetween(HostPlayer(), ShownLocation) < 35 && (IsInViewAngle(HostPlayer(), ShownLocation, 50) || DistanceBetween(HostPlayer(), ShownLocation) < 5))
            Show();
        else
            Hide();
    }
}

rule: "Connect All Segments"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.SecondaryFire))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    for (define f = 0; f < Selected.Length; f++)
        for (define l = 0; l < Selected.Length; l++)
            if ((l != f) && !SegmentExists(Selected[f], Selected[l]))
            {
                ModifyVariable(Segments, Operation.AppendToArray, new Segment(Selected[f], Selected[l], FirstAttribute, SecondAttribute));
                Segments.Last.Show();
            }
}

rule: "Connect Segment Path"
Event.OngoingPlayer
if (!FreeWalk)
if (IsCommunicating(EventPlayer(), Communication.VoiceLineRight))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    for (define i = 1; i < Selected.Length; i++)
        if (!SegmentExists(Selected[i - 1], Selected[i]))
        {
            ModifyVariable(Segments, Operation.AppendToArray, new Segment(Selected[i - 1], Selected[i], FirstAttribute, SecondAttribute))
            Segments.Last.Show();
        }
}

rule: "Connect Segments From First"
Event.OngoingPlayer
if (!FreeWalk)
if (IsCommunicating(EventPlayer(), Communication.VoiceLineLeft))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    for (define i = 1; i < Selected.Length; i++)
        if (!SegmentExists(Selected[0], Selected[i]))
        {
            ModifyVariable(Segments, Operation.AppendToArray, new Segment(Selected[0], Selected[i], FirstAttribute, SecondAttribute))
            Segments.Last.Show();
        }
}

define SegmentExists(define node1, define node2): Segments.IsTrueForAny(Segment segment => segment.Nodes.Contains(node1) && segment.Nodes.Contains(node2));

rule: "Delete Segments"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.SecondaryFire))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    for (define i = Segments.Length - 1; i >= 0; i--)
        if (Segments[i].Nodes.IsTrueForAll(Node node => Selected.Contains(node)))
        {
            Segments[i].Hide();
            delete(Segments[i]);
            ModifyVariable(Segments, Operation.RemoveFromArrayByIndex, i);
        }
}

class Segment
{
    public Node Node1;
    public Node Node2;    
    public Node[] Nodes;
    public define FirstAttribute;
    public define SecondAttribute;

    define EffectID;
    public define visible = false;

    public define AttributeText1;
    public define AttributeText2;

    public Segment(Node node1, Node node2, define firstAttribute, define secondAttribute)
    {
        Node1 = node1;
        Node2 = node2;
        Nodes = [Node1, Node2];
        FirstAttribute = firstAttribute;
        SecondAttribute = secondAttribute;
    }

    public void Show()
    {
        if (!visible)
        {
            CreateBeamEffect(AllPlayers(), BeamType.GrappleBeam, Node1.ShownLocation, Node2.ShownLocation, Color.Red, EffectRev.VisibleTo);
            EffectID = LastCreatedEntity();
            visible = true;

            if (FirstAttribute != 0 || SecondAttribute != 0)
            {
                CreateInWorldText(
                    AllPlayers(),
                    FirstAttribute,
                    LinearInterpolate(
                        Node1.ShownLocation,
                        Node2.ShownLocation,
                        0.2
                    ),
                    2.5,
                    Clipping.ClipAgainstSurfaces,
                    InworldTextRev.VisibleTo,
                    Color.Yellow,
                    Spectators.VisibleAlways
                );
                AttributeText1 = LastTextID();
                CreateInWorldText(
                    AllPlayers(),
                    SecondAttribute,
                    LinearInterpolate(
                        Node1.ShownLocation,
                        Node2.ShownLocation,
                        0.8
                    ),
                    2.5,
                    Clipping.ClipAgainstSurfaces,
                    InworldTextRev.VisibleTo,
                    Color.Orange,
                    Spectators.VisibleAlways
                );
                AttributeText2 = LastTextID();
            }
        }
    }
    public void Hide()
    {
        if (visible)
        {
            DestroyEffect(EffectID);
            DestroyInWorldText(AttributeText1);
            DestroyInWorldText(AttributeText2);
            visible = false;
        }
    }
}



rule: "Update visible"
if (updateVisibleLook == null || DistanceBetween(updateVisiblePosition, HostPlayer()) > 3 || AngleBetweenVectors(updateVisibleLook, FacingDirectionOf(HostPlayer())) > 10)
{
    updateVisibleLook = FacingDirectionOf(HostPlayer());
    updateVisiblePosition = PositionOf(HostPlayer());

    foreach (Segment segment in Segments)
    {
        segment.Node1.UpdateVisible();
        segment.Node2.UpdateVisible();

        if ((segment.Node1.visible) || segment.Node2.visible)
            segment.Show();
        else
            segment.Hide();
        
        MinWait();
    }
    Wait(0.2);
    LoopIfConditionIsTrue();
}

rule: "CSV out"
Event.OngoingPlayer
if (IsButtonHeld(EventPlayer(), Button.Melee))
{
    compiling = true;
    SmallMessage(AllPlayers(), "Compiling...");

    nodesOut = EmptyArray();
    segmentsOut = EmptyArray();

    foreach (Node node in Nodes)
        ModifyVariable(nodesOut, Operation.AppendToArray, node.Pos);
    
    foreach (Segment segment in Segments)
        ModifyVariable(segmentsOut, Operation.AppendToArray, Vector(
            // X is first node
            IndexOfArrayValue(Nodes, segment.Node1) + (segment.FirstAttribute / 100),
            // Y is second node
            IndexOfArrayValue(Nodes, segment.Node2) + (segment.SecondAttribute / 100),
            // Z is unused
            0
        ));

    SmallMessage(AllPlayers(), "Compile Finished!");
    EnableInspectorRecording();
    # Pathmap is compiled. Copy the current global variable set to the clipboard. In vscode, press ctrl+shift+p to open the command pallete, then run the 'Create pathmap from CSV clipboard' command.
    DisableInspectorRecording();
    compiling = false;
}

rule: "Preload"
if (CountOf(preloadNodes) != 0)
{
    // Preload nodes
    foreach (Vector node in preloadNodes)
        ModifyVariable(Nodes, Operation.AppendToArray, new Node(node));
    
    // Preload segments
    foreach (Vector segment in preloadSegments)
        ModifyVariable(Segments, Operation.AppendToArray, new Segment(
            Nodes[RoundToInteger(segment.X, Rounding.Down)],
            Nodes[RoundToInteger(segment.Y, Rounding.Down)],
            (segment.X % 1) * 100,
            (segment.Y % 1) * 100
        ));
}

rule: "Change Primary Attribute"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.Ability1))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    FirstAttribute++;
}
rule: "Change Secondary Attribute"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.Ability2))
if (!IsButtonHeld(EventPlayer(), Button.Crouch))
{
    SecondAttribute++;
}
rule: "Reset Primary Attribute"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.Ability1))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    FirstAttribute = 0;
}
rule: "Reset Secondary Attribute"
Event.OngoingPlayer
if (!FreeWalk)
if (IsButtonHeld(EventPlayer(), Button.Ability2))
if (IsButtonHeld(EventPlayer(), Button.Crouch))
{
    SecondAttribute = 0;
}

rule: "Create Selected ID Text"
Event.OnDamageTaken
if (scoper == EventPlayer())
if (ScopeID == 1)
{
    CreateInWorldText(
        AllPlayers(),
        IndexOfArrayValue(Nodes, ScopeData),
        (<Node>ScopeData).ShownLocation,
        3,
        Clipping.DoNotClip,
        InworldTextRev.String,
        Color.Green,
        Spectators.VisibleAlways
    );
    (<Node>ScopeData).SelectIDText = LastTextID();
}